#!/bin/tcsh

set TPTP4X="/exp/home/tptp/ServiceTools/TPTP4XDir/tptp4X"
set BNFParser="/exp/home/tptp/TPTP/SyntaxBNF/SyntaxBNF-yl-parser-verbose"

set KeepAllFiles=0
set KeepFailedFiles=0
set KeepWarnedFiles=0
set FailedAnything=0
set WarnedAnything=0

if ($1 == "--keep_all") then
    set KeepAllFiles=1
    shift
else 
    if ($1 == "--keep_failed") then
        set KeepFailedFiles=1
        shift
    else 
        if ($1 == "--keep_warned") then
            set KeepFailedFiles=1
            set KeepWarnedFiles=1
            shift
        endif
    endif
endif

set FileName=`basename $1`
set OutputDir="TestOutput/$FileName"
rm -rf $OutputDir
mkdir -p $OutputDir
cp $1 $OutputDir

#----Test if formatting changes anything
echo "TRYING: Formatting    Original    version            for $FileName"
$TPTP4X $1 > $OutputDir/$FileName.formatted
if (`diff -q $1 $OutputDir/$FileName.formatted | wc -l`) then
    echo "WARNED: Original  and Formatted   versions   changed for $FileName"
    set WarnedAnything=1
#----Check if the changed formatting is innocuous, by looking at parse trees
    echo "TRYING: Parsing       Original    version            for $FileName"
    $BNFParser < $OutputDir/$FileName | sed -e 's/^[^0-9]*[0-9] //' > $OutputDir/$FileName.parsed
    echo "TRYING: Parsing       Formatted   version            for $FileName"
    $BNFParser < $OutputDir/$FileName.formatted | sed -e 's/^[^0-9]*[0-9] //' > $OutputDir/$FileName.formatted.parsed
    diff --suppress-common-lines --side-by-side --expand-tabs $OutputDir/$FileName.parsed $OutputDir/$FileName.formatted.parsed > $OutputDir/$FileName.parsed_diff
    TestScriptCleanBrackets.awk < $OutputDir/$FileName.parsed_diff > $OutputDir/$FileName.parsed_diff.cleaned
    if (`cat $OutputDir/$FileName.parsed_diff.cleaned | wc -l`) then
#----Shitto shitto, the parse trees are different, so the formatting broke something
        echo "FAILED: Original  and Formatted   parses     changed for $FileName"
        set FailedAnything=1
#----Check if formatting is a fixed point
        echo "TRYING: Formatting    Formatted   version            for $FileName"
        $TPTP4X $OutputDir/$FileName.formatted > $OutputDir/$FileName.formatted.formatted
        if (`diff $OutputDir/$FileName.formatted $OutputDir/$FileName.formatted.formatted | wc -l`) then
#----Damn, reformatting changed it too. Very bad.
            echo "FAILED: Formatted and Reformatted versions   changed for $FileName"
            set FailedAnything=1
        else
            echo "PASSED: Formatted and Reformatted versions unchanged for $FileName"
        endif
    else
        echo "PASSED: Original  and Formatted   parses   unchanged for $FileName"
    endif
else
    echo "PASSED: Original  and Formatted   versions unchanged for $FileName"
endif

if ($KeepAllFiles || ($KeepFailedFiles && $FailedAnything) || \
($KeepWarnedFiles && ($FailedAnything || $WarnedAnything))) then
    echo "Files saved in $OutputDir"
else
    echo "Deleting $OutputDir"
    rm -rf $OutputDir
endif
