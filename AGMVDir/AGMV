#!/bin/tcsh

set TPTP4X = "$TPTP_HOME/ServiceTools/tptp4X"
set AGMMV = "$TPTP_HOME/ServiceTools/AGMVDir/AGMMV"
set GCMV = "$TPTP_HOME/ServiceTools/AGMVDir/GCMV"
set TempDir = "/tmp/AGMV_$$"
set MinimalPerFormulaCPULimit = 10

#----Check that there is a problem file and an interpretation file
if ($#argv < 2) then
    echo "Usage: $0 <time_limit> <problem file> <interpretation file>"
    echo "   or: $0 <time_limit> <problem and interpretation file>"
    exit
else
    set CPULimit = $1
    if ($#argv == 3) then
        set ProblemFile = $2
        set ModelFile = $3
    else
        onintr cleanup
        mkdir -p $TempDir
        set ProblemFile = "$TempDir/AGMVProblemFile_$$"
        set ModelFile = "$TempDir/AGMVModelFile_$$"
        $TPTP4X -t uniquenames4 -u machine $2 | grep -E '^ *..f\([^,]*, *(type|axiom|hypothesis|definition|conjecture) *,' > $ProblemFile
        $TPTP4X -t uniquenames4 -u machine $2 | grep -E '^ *..f\([^,]*, *(type|interpretation|fi_[a-z]*) *,' > $ModelFile
#DEBUG echo "---- Problem file ----"
#DEBUG cat $ProblemFile
#DEBUG echo "---- Interpretation file ----"
#DEBUG cat $ModelFile
#DEBUG echo "----------------- ----"
#DEBUG goto cleanup
    endif
endif

#----Check if classical or modal
if (`grep -c '$ki_world' $ModelFile`) then
    echo "% This is a Kripke model, using modal verification"
    $AGMMV $CPULimit $ProblemFile $ModelFile
else
    echo "% This is a Tarskian model, using classical verification"
    $GCMV -t $CPULimit $ProblemFile $ModelFile
endif

cleanup:
echo "% AGMV ended, cleaning up"
rm -rf $TempDir
